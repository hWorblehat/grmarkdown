


plugins {
	id 'java-gradle-plugin'
	id 'groovy'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.6.3'
}

allprojects {
	repositories {
		jcenter()
	}
}

sourceSets {
	transformer
	base {
		compileClasspath += transformer.output
	}
	[main, test].each {
		it.compileClasspath += base.output
		it.runtimeClasspath += base.output
	}
}

configurations {
	compile.extendsFrom baseCompile
	runtime.extendsFrom baseRuntime
}
	
dependencies {
	transformerCompile (
		'org.codehaus.groovy:groovy-all:2.4.4',
		'org.pegdown:pegdown:1.5.+'
	)
	
	baseCompile (
		gradleApi(),
		'com.google.guava:guava:19.+',
		'commons-io:commons-io:2.+',
		'org.codehaus.groovy:groovy-all:2.4.4',
		'org.pegdown:pegdown:1.5.+'
	)

	testCompile (
		'junit:junit:4.12',
		'org.spockframework:spock-core:1.0-groovy-2.4'
	)
}

jar {
	from sourceSets.base.output
}

jacocoTestReport {
	additionalClassDirs sourceSets.base.output
	additionalSourceDirs sourceSets.base.allJava
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

coveralls {
	sourceDirs += sourceSets.base.allJava.srcDirs.collect{it.path}
}

tasks.coveralls {
	dependsOn jacocoTestReport
}

gradlePlugin {
	plugins {
		base {
			id = 'org.uulib.grmd.markdown-base'
			implementationClass = 'org.uulib.grmd.plugin.MarkdownBasePlugin'
		}
		traditional {
			id = 'org.uulib.grmd.markdown'
			implementationClass = 'org.uulib.grmd.plugin.MarkdownPlugin'
		}
		model {
			id = 'org.uulib.grmd.markdown-lang'
			implementationClass = 'org.uulib.grmd.plugin.MarkdownModelPlugin'
		}
	}
}
/*
 * Copyright (c) 2016 Rowan Lonsdale.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

sourceSets {
	transformer
	base {
		compileClasspath += transformer.output
	}
	[main, test].each {
		it.compileClasspath += base.output
		it.runtimeClasspath += base.output
	}
}

configurations {
	compile.extendsFrom baseCompile
	runtime.extendsFrom baseRuntime
}
	
dependencies {
	transformerCompile (
		'org.codehaus.groovy:groovy-all:2.4.4',
		'org.pegdown:pegdown:1.5.+'
	)
	
	baseCompile (
		gradleApi(),
		'com.google.guava:guava:19.+',
		'commons-io:commons-io:2.+',
		'org.codehaus.groovy:groovy-all:2.4.4',
		'org.pegdown:pegdown:1.5.+'
	)

	testCompile (
		'junit:junit:4.12',
		'org.spockframework:spock-core:1.0-groovy-2.4'
	)
}

jar {
	from sourceSets.base.output
}

String j2seDocsURL = 'http://docs.oracle.com/javase/7/docs/api/'
String gradleDocsURL = "https://docs.gradle.org/${gradle.gradleVersion}"
String pegdownDocsURL = "http://www.javadoc.io/doc/org.pegdown/pegdown/1.6.0"

Closure javadocOptions =  {
	use         = true
	windowTitle = "$project.name $project.version API"
	docTitle    = "$project.name $project.version API"
}

javadoc {
	source(sourceSets.transformer.allJava)
	source(sourceSets.base.allJava)
	source(sourceSets.main.allJava)
	
	classpath = sourceSets.main.compileClasspath
	
	javadocOptions.setDelegate(options)
	javadocOptions.call()
	
	options.encoding    = 'UTF-8'
	options.author      = true
	options.version     = true
	options.links(j2seDocsURL, "${gradleDocsURL}/javadoc/", "${gradleDocsURL}/groovydoc/", pegdownDocsURL)
}

groovydoc {
	source(sourceSets.transformer.java)
	source(sourceSets.base.allJava)
	source(sourceSets.base.groovy)
	source(sourceSets.main.java)
	
	classpath = sourceSets.main.compileClasspath
	
	javadocOptions.setDelegate(delegate)
	javadocOptions.call()
	
	link(j2seDocsURL, 'java')
	link("${gradleDocsURL}/javadoc/", 'org.gradle')
	link("${gradleDocsURL}/groovydoc/", 'org.gradle')
	link(pegdownDocsURL, 'org.pegdown')
}